module asm-x86

imports
  
  Common

context-free start-symbols
  
  Start

sorts

  Start File

context-free syntax
  
  Start = File 
  
  File.File = [
    [Globals]
    [Sections]
  ]
  
  
  File.Empty = []
  
  Globals.NoGlobals = 
  Globals = [global [{Global ","}+]]
  Global.Global   = ID
  
  Sections = {Section "\n"}*
  
  Section.DataSection = [
    SECTION .data
    [DataDeclaration]
  ]
  
  DataDeclaration.DataDeclaration = [[ID]: [DefineMultiplier?] [DefineType] [DefineInitializer]]

  DefineMultiplier.DefineMultiplier = [TIMES [INT]]
  
  DefineType.Ten        = [dt]
  DefineType.Quadword   = [dq]
  DefineType.Doubleword = [dd]
  DefineType.Word       = [dw]
  DefineType.Byte       = [db]
  
  DefineInitializer.Initializer = INT
    
  Section.CodeSection = [
    SECTION .code
    [Instructions]
  ]
  
  Instructions.NoInstructions = []
  Instructions = {Instruction "\n"}+
  
  Instruction.Label = [[ID]:]
  Instruction.Push  = [push [Operand]]
  Instruction.Pop   = [pop [Operand]]
  Instruction.Mov   = [mov [Operand], [Operand]]
  Instruction.Sub   = [sub [Operand], [Operand]]
  Instruction.Test  = [test [Operand], [Operand]]
  Instruction.Cmp   = [cmp [Operand], [Operand]]
  Instruction.Jle   = [jle [ID]]
  Instruction.Jmp   = [jmp [ID]]
  
  Operand.DWPointer = <DWORD [<Expression>]>
  Operand.Expression = Expression
  Operand.Register = Register

  Expression.Sub = [[Expression] - [Expression]]
  Expression = Register
  Expression = Value

  Value.Const = INT
  
  Register.RBP = [rbp]
  Register.RSP = [rsp]
  Register.EAX = [eax]
  Register.EBX = [ebx]
    